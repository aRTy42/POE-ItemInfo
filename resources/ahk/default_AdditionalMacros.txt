   Pause::TogglePOEItemScript()		; pause item parsing with the pause key (other macros remain).
   #D::WinMinimize, A				; Winkey+D minimizes the active PoE window (PoE stays minimized this way).
   
   ^F::HighlightItems(false,true,false)		; Ctrl+F fills search bar in the stash or vendor screens with item's name or info you're hovering over.
											; Function parameters, change if needed or wanted:
											;	1. Use broader terms, default = false.
											;	2. Leave the search field after pasting the search terms, default = true.
											;	3. Add a space after every opening quotation mark, default = false. (Some keyboard layouts need this to prevent adding special characters, Dutch for example.)
   ^!F::HighlightItems(true,true,false)		; Ctrl+Alt+F uses much broader search terms for the highlight function.
   
;   ^Escape::CloseScripts()			; Ctrl+Esc closes all running scripts specified by (and including) ItemInfo or TradeMacro.
  
;   F4::SendInput {Enter}/kick own_char_name{Enter}		; quickly leave a group by kicking yourself. Only works for one specific character name.
   F5::SendInput {Enter}/hideout{Enter}			; go to hideout with F5
   ^WheelDown::SendInput {Right}				; Ctrl+scroll down scrolls through stash tabs rightward
   ^WheelUp::SendInput {Left}					; Ctrl+scroll up scrolls through stash tabs leftward
   ~RButton & WheelDown::Send {Right}			; holding right mouse button+scroll down scrolls through stash tabs rightward
   ~RButton & WheelUp::Send {Left}				; holding right mouse button+scroll up scrolls through stash tabs leftward

;   ^RButton::SendInput ^c						; Ctrl+right mouse button sends ctrl+c
;   F9::SendInput {Enter}/remaining{Enter}		; mobs remaining with F9
;   F10::SendInput {Enter}/global 820{Enter}	; join a channel with F10
;   F11::setAfkMessage()						; pastes afk message to your chat and marks "X" so you can type in the estimated time

setAfkMessage(){		
	T1 := A_Now
	T2 := A_NowUTC
	EnvSub, T1, %T2%, M
	TZD := "UTC +" Round( T1/60, 2 )
	FormatTime, currentTime, A_NowUTC, HH:mm
	clipboard := "/afk AFK for about X minutes, since " currentTime " (" TZD "). Leave a message and I'll reply."
	
	IfWinActive, Path of Exile ahk_class POEWindowClass 
	{
		SendInput {Enter} ^{v} {Home}
		Pos := RegExMatch(clipboard, " X ")
		If (Pos) {
			Loop {
				SendInput {Right}
				If (A_Index > Pos) {
					Break
				}
			}
			Send {Shift Down} 
			Sleep 100
			Send {Right} 
			Sleep 100
			Send {Shift Up} 
		}
	}		
}